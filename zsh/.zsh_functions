# `tre` is a shorthand for `tree` with hidden files and color enabled, ignoring
# the `.git` directory, listing directories first. The output gets piped into
# `less` with options to preserve color and line numbers, unless the output is
# small enough for one screen.
function tre() {
  tree -aC -I '.git|node_modules|bower_components' --dirsfirst "$@" | less -FRNX
}

cx() { cd "$@" && ll; }

function nuke() {
  kill $(ps aux | grep '[A]dobe' | awk '{print $2}')
  kill $(ps aux | grep '[F]orti[C]lient' | awk '{print $2}')
  kill $(ps aux | grep '[L]ogitech' | awk '{print $2}')
  kill $(ps aux | grep 'toolbox-helper' | awk '{print $2}')
}

envup() {
  local file=$([ -z "$1" ] && echo ".env" || echo ".env.$1")

  if [ -f $file ]; then
    set -a
    source $file
    set +a
  else
    echo "No $file file found" 1>&2
    return 1
  fi
}

awss() {
  export AWS_PROFILE=$1
  echo "Using AWS_PROFILE=$1"
}
_awss() {
  local curcontext="$curcontext" state line
  typeset -A opt_args

  _arguments '1: :->csi'

  case $state in
  csi)
    _arguments "1: :($(aws configure list-profiles))"
    ;;
  esac
}
compdef _awss awss

random_phrase() {
  # Define an array of phrases
  local short_phrases=(
    "Change is the only constant"
    "Stay hungry, stay foolish"
    "Failure teaches success"
    "Think big, start small"
    "Practice makes perfect"
    "Keep your commitments"
    "Knowledge is power"
    "Grow 1% every day"
  )

  local long_phrases=(
    "Be productive early. Do not fuck around all day"
    "Be fucking practical. Success is not a theory"
    "Stop bullshitting. It is fucking embarrassing"
    "Care about the process, not just the outcome"
    "Acquire new knowledge and always ask why"
    "Hope for the best, prepare for the worst"
    "Keep it simple, stupid (KISS principle)"
    "Do the fucking work. Do not be lazy"
    "Stop fucking waiting. It is time"
    "Actions speak louder than words"
    "Fail by action, not inaction"
  )

  local cols=$(tput cols)

  local phrases_to_use=()
  if ((cols < 90)); then
    phrases_to_use=("${short_phrases[@]}")
  else
    phrases_to_use=("${short_phrases[@]}" "${long_phrases[@]}")
  fi

  random_phrase=$(printf "%s\n" "${phrases_to_use[@]}" | shuf -n 1)

  figlet -w $cols "$random_phrase" | lolcrab -g cool
}

function change-commit-date() {
  export GIT_AUTHOR_DATE=$(gdate -d"$1" --rfc-email)
  export GIT_COMMITTER_DATE=$GIT_AUTHOR_DATE

  echo "Changing date to ${GIT_AUTHOR_DATE}"
  git commit --amend --no-edit --date "${GIT_AUTHOR_DATE}"
}

extract() {
  if [ -f $1 ]; then
    case $1 in
    *.tar.bz2) tar -xvjf $1 ;;
    *.tar.gz) tar -xvzf $1 ;;
    *.tar.xz) tar -xvJf $1 ;;
    *.bz2) bunzip2 $1 ;;
    *.rar) rar x $1 ;;
    *.gz) gunzip $1 ;;
    *.tar) tar -xvf $1 ;;
    *.tbz2) tar -xvjf $1 ;;
    *.tgz) tar -xvzf $1 ;;
    *.zip) unzip $1 ;;
    *.Z) uncompress $1 ;;
    *.7z) 7z x $1 ;;
    *) echo "don't know how to extract '$1'..." ;;
    esac
  else
    echo "'$1' is not a valid file!"
  fi
}

function tailwatch() {
  aws logs tail "$1" --follow --format short
}

rrmdir() {
  if [ $# -eq 0 ]; then
    echo "Usage: rrmdir <directory_name_1> [<directory_name_2> ...]"
    return 1
  fi

  local dir_names=("$@")
  local name_pattern=""

  for dir_name in "${dir_names[@]}"; do
    find . -type d -name $dir_name -prune -exec rm -rf "{}" +
  done
}

# Fzf functions from https://github.com/i05nagai/dotfiles/blob/ca3d717739dc382878abde9ec3e79174aac5eb79/shell/fzf.sh
fzf_select_history() {
  BUFFER=$(history -n -r 1 | fzf --no-sort +m --query "$LBUFFER" --prompt="History > ")
  CURSOR=$#BUFFER
}

fzf_git_commit_fixup() {
  local commits commit sha1
  commits=$(git log --color=always --pretty=oneline --abbrev-commit --reverse) &&
    commit=$(echo "$commits" | fzf --tac +s +m -e --ansi --reverse) &&
    git commit --fixup=$(echo "$commit" | sed "s/ .*//")
}

fzf_connect_ssh() {
  local host="$(egrep -i '^Host\s+.+' $HOME/.ssh/config \
    $(find $HOME/.ssh/conf -type f 2>/dev/null) |
    egrep -v '[*?]' |
    awk '{print $2}' |
    sort |
    fzf)"

  ssh "$host"
}

fzf_aliases() {
  local alias_name="$(alias |
    fzf --tac +s -m -e --ansi |
    awk -F '=' '{print $1}')"
  echo -n "${alias_name}"
}

git_change_remote() {
  # Usage: git_change_remote [ssh|https]
  # Default behavior: toggles between ssh and https

  # Get current remote URL
  url=$(git remote get-url origin 2>/dev/null)
  if [ -z "$url" ]; then
    echo "Error: Could not retrieve remote URL. Are you inside a Git repository?"
    return 1
  fi

  # Determine current protocol and set target protocol
  if [ "$1" = "ssh" ] || [ "$1" = "https" ]; then
    target_proto="$1"
  else
    # Extract protocol from URL
    case "$url" in
    https://*)
      current_proto="https"
      target_proto="ssh"
      ;;
    git@*)
      current_proto="ssh"
      target_proto="https"
      ;;
    *)
      echo "Error: Unknown URL format: $url"
      return 1
      ;;
    esac
  fi

  # Check if already using the target protocol
  case "$url" in
  "$target_proto"*)
    echo "Already using $target_proto protocol."
    return 0
    ;;
  esac

  # Convert the URL to the target protocol
  if [ "$target_proto" = "ssh" ]; then
    # Convert HTTPS to SSH
    new_url=$(printf '%s\n' "$url" | sed -E 's#https://([^/]+)/(.+)#git@\1:\2#')
  else
    # Convert SSH to HTTPS
    new_url=$(printf '%s\n' "$url" | sed -E 's#git@([^:]+):(.+)#https://\1/\2#')
  fi

  # Set the new remote URL
  if [ -n "$new_url" ]; then
    echo "Changing remote URL from:"
    echo "  $url"
    echo "to:"
    echo "  $new_url"
    git remote set-url origin "$new_url"
  else
    echo "Error: Failed to generate new URL."
    return 1
  fi
}
